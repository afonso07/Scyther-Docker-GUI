usertype Sessionkey;
usertype Ticket;
usertype Timestamp;
secret k: Function;

protocol kaochow3(I,R,S)
{
	role I
	{
		fresh ni: Nonce;
		var nr: Nonce;
		var kir,kt: Sessionkey;
		var T2: Ticket;

		send_1 (I,S, I,R,ni);
		recv_3 (R,I, R, {I,R,ni,kir,kt}k(I,S), {ni, kir}kt, nr, T2 );
		send_4 (I,R, {nr,kir}kt, T2 );

		claim_5 (I, Nisynch);
		claim_6 (I, Niagree);
		claim_7 (I, Secret, kir);
	}	
	
	role R
	{
		var ni: Nonce;
		fresh nr: Nonce;
		var kir,kt: Sessionkey;
		var T: Ticket;
		fresh tr: Timestamp;

		recv_2 (S,R, T, { I,R,ni,kir,kt }k(R,S)  ); 
		send_3 (R,I, R, T, {ni, kir}kt, nr, {I,R,tr,kir}k(R,S) );
		recv_4 (I,R, {nr,kir}kt, {I,R,tr,kir}k(R,S) );

		claim_8 (R, Nisynch);
		claim_9 (R, Niagree);
		claim_10 (R, Secret, kir);
	}

	role S
	{
		var ni: Nonce;
		fresh kir, kt: Sessionkey;

		recv_1 (I,S, I,R,ni);
		send_2 (S,R, {I,R,ni,kir,kt}k(I,S), { I,R,ni,kir,kt }k(R,S)  ); 
	}
}


const te: Ticket;
const ke: Sessionkey;

