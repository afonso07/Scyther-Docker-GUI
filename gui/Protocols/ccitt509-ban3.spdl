# BAN modified version of CCITT X.509 (3)
#
# Modelled after the description in the SPORE library
# http://www.lsv.ens-cachan.fr/spore/ccittx509_3BAN.html
#
# Note:
# The protocol description also states that Xa and Ya should be fresh
# this can not be verified using scyther
# 
# Note:
# According to SPORE there are no known attacks on this protocol
#

protocol ccitt509-ban3(I,R)
{
    role I
    {
        fresh Na,Xa,Ya: Nonce;
        var Xb,Nb,Yb: Nonce;

        send_1(I,R, I,{Na, R, Xa,{Ya}pk(R)}sk(I));
        recv_2(R,I, R,{Nb, I, Na, Xb,{Yb}pk(I)}sk(R));
        send_3(I,R, I,{R, Nb}sk(I));
        claim_4(I,Nisynch);
    }    
    
    role R
    {
        var Na,Xa,Ya: Nonce;
        fresh Xb,Yb,Nb: Nonce;

        recv_1(I,R, I,{Na, R, Xa,{Ya}pk(R)}sk(I));
        send_2(R,I, R,{Nb, I, Na, Xb,{Yb}pk(I)}sk(R));
        recv_3(I,R, I,{R, Nb}sk(I));
        claim_5(R,Nisynch);
        # There should also be Fresh Xa and Fresh Ya claims here
    }
}

