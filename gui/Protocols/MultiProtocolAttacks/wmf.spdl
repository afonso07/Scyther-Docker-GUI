# Wide Mouthed Frog
#
# Modelled after the description in the SPORE library
# http://www.lsv.ens-cachan.fr/spore/wideMouthedFrog.html
#
# Note
# The name of the party that has generated a message was added in order
# to model the property described in SPORE that an agent can identify
# its own messages and will reject them.

usertype SessionKey;
usertype TimeStamp;
usertype ExpiredTimeStamp;

const Fresh: Function;
const Compromised: Function;

protocol wmf(I,R,S)
{
    role I
    {
        fresh Kir: SessionKey;
        fresh Ti: TimeStamp;
        var Kr: SessionKey;

        send_1(I,S, I, {I, Ti, R, Kir}k(I,S)); 

        claim_I1(I,Secret,Kir);
        claim_I2(I,Empty,(Fresh,Kir));
    }    
    
    role R
    {
        var Ts: TimeStamp;
        var Kir: SessionKey;

        recv_2(S,R, {S, Ts, I, Kir}k(R,S) );

        claim_R1(R,Secret,Kir);
        claim_R2(R,Nisynch);
        claim_R3(R,Empty,(Fresh,Kir));
    }

    role S
    {
        var Kir: SessionKey;
        fresh Ts: TimeStamp;
        var Ti: TimeStamp;

        recv_1(I,S, I,{I, Ti, R, Kir}k(I,S) );
        send_2(S,R, {S, Ts, I, Kir}k(R,S));
    }
}

